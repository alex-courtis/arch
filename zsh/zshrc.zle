zmodload zsh/complist

if [ -n "${WAYLAND_DISPLAY}" -o -n "${DISPLAY}" ]; then
	. /usr/share/zsh/plugins/zsh-system-clipboard/zsh-system-clipboard.zsh > /dev/null 2>&1
fi

# vim zle mode
bindkey -v

# not using any esc-something sequences so drop the timeout from 40 for better editing responsiveness
KEYTIMEOUT=1

# with proper deletes
bindkey -M viins "${terminfo[kdch1]}" delete-char
bindkey -M vicmd "${terminfo[kdch1]}" delete-char

# shift tab for reverse menu only; tab is still set to expand-or-complete
bindkey -M viins "^[[Z" reverse-menu-complete

# completes the current and tries to complete again; useful for multi level directories
bindkey -M menuselect "/" accept-and-infer-next-history

# easy navigation
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history

# search up to cursor
bindkey -M viins "^J" history-beginning-search-forward
bindkey -M viins "^K" history-beginning-search-backward

# incremental search which uses a limited set of regular bindings, overridden by the (empty) isearch map
bindkey -M viins "^F" history-incremental-search-forward
bindkey -M viins "^B" history-incremental-search-backward

# vim cursor style; using the same VTE values as vim terminus plugin for these nonstandard extensions
CUR_BLOCK="\033[2 q"
CUR_UNDERSCORE="\033[4 q"
CUR_BEAM="\033[6 q"
function updatecursor() {
	case "${KEYMAP}" in
		(vicmd)
			printf "${CUR_BLOCK}"
			;;
		(*)
			if [[ "${ZLE_STATE}" =~ "overwrite" ]]; then
				printf "${CUR_UNDERSCORE}"
			else
				printf "${CUR_BEAM}"
			fi
			;;
	esac
}
zle -N zle-line-init updatecursor
zle -N zle-keymap-select updatecursor
function resetcursor() {
	printf "${CUR_BLOCK}"
}
zle -N zle-line-finish resetcursor

# abort on ^C, which detached from stty intr between precmd and preexec
function abort-buffer() {
	BUFFER="${BUFFER}"
	zle send-break
}
zle -N abort-buffer
bindkey -M viins "^C" abort-buffer
bindkey -M vicmd "^C" abort-buffer

# explicitly define this behaviour which was implicitly gained vi stty intr
bindkey -M command "^C" send-break

# disable XON/XOFF flow control; ctrl-S suspend ctrl-Q resume are not useful
stty -ixon

